plugins {
    id "java"
    id "maven-publish"
}

group = "com.github.apatchydev"
sourceCompatibility = "11"
version "1.0.0"

ext {
    verLogback = "1.4.0"
    verJackson = "2.14.0"

    verJUnit = "5.3.1"
    verAssertj = "3.24.2"
}

repositories {
    mavenCentral()
}

sourceSets {
    example {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    exampleImplementation.extendsFrom implementation
    exampleRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    implementation "ch.qos.logback:logback-classic:$verLogback"
    implementation "com.fasterxml.jackson.core:jackson-databind:$verJackson"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$verJUnit"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$verJUnit"
    testImplementation "org.assertj:assertj-core:$verAssertj"
}

tasks.register("example", JavaExec)

tasks.named("test") {
    useJUnitPlatform()
}

tasks.named("example") {
    dependsOn "exampleClasses"
    classpath sourceSets.example.runtimeClasspath
    mainClass = "com.github.apatchydev.logbackextra.ExampleApp"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "com.github.apatchydev"
            artifactId = "logback-extra"
            version = project.version

            from components.java
        }
    }
}
